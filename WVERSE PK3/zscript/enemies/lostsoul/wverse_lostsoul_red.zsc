//===========================================================================
//
// Lost Soul (Red)
// A lost soul with minimal behavior changes.
// Originally featured in The Space Pirate
//
// CODE: Cardboard Marty, adapted from code by Ghastly_dragon
// SPRITES: Captain Toenail
//
//===========================================================================
class WVerse_LostSoul : Actor
{
	Default
	{
		Health 100;
		Radius 16;
		Height 56;
		Mass 50;
		Speed 3;
		Damage 3;
		PainChance 256;
		Bloodtype "None";
		Monster;
		+FLOAT
		+NOGRAVITY
		+MISSILEMORE
		+DONTFALL
		+NOICEDEATH
		+ZDOOMTRANS
		+RETARGETAFTERSLAM
		SeeSound "lostsoul/sight";
		PainSound "lostsoul/pain";
		DeathSound "lostsoul/death";
		ActiveSound "lostsoul/active";
	}
}

class WVerse_LostSoul_Red : WVerse_LostSoul
{
	Default
	{
		Obituary "$WVERSE_LOSTSOUL_RED_OBIT";
		Tag "Lost Soul (Red)";
	}
	States
	{
	Spawn:
		1FOR AB 10 BRIGHT A_Look;
		1FOR A 0 BRIGHT A_Jump(16, 1);
		Loop;
		1FOR A 0 BRIGHT A_StartSound("lostsoul/active", CHAN_AUTO);
		Loop;
	See:
		1FOR AB 6 BRIGHT A_Chase;
		Loop;
	Missile:
		1FOR C 10 BRIGHT A_FaceTarget;
		1FOR D 0 BRIGHT A_StartSound("lostsoul/sight", CHAN_AUTO);
		1FOR D 4 BRIGHT A_SkullAttack;
		1FOR CCCC 1 BRIGHT;
		1FOR C 0 A_Jump(16, 6);
		1FOR DDDD 1 BRIGHT;
		1FOR D 0 A_Jump(16, 1);
		Goto Missile+4;
		1FOR C 0;
		Goto Missile+2;
	Pain:
		1FOR E 3 BRIGHT;
		1FOR E 3 BRIGHT A_Pain;
		Goto See;
	Death:
		1FOR EF 4 BRIGHT;
		1FOR G 6 BRIGHT A_Scream;
		1FOR G 0 BRIGHT A_StartSound("lostsoul/explode", CHAN_AUTO);
		1FOR H 6 BRIGHT;
		1FOR I 6 BRIGHT A_NoBlocking;
		1FOR J 6;
		Stop;
	}
}