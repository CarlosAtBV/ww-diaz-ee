// pistol guy - now based on Zombie Henchman, by Captain Red, Xim, and CthulhuCultist

actor WeaselPistolZombie
{
	obituary "%o received a slug to the chest."
	health 20
	height 56
	mass 100
	speed 8
	painchance 200
	seesound "halomarine/sighted"
	attacksound "colt45/fire"
	painsound "halomarine/pain"
	deathsound "halomarine/death"
	activesound "combine/idle"
	dropitem Clip 128//P50Ammo 128
	DropItem P50Handgun 64
	Tag "Possessed Civilian"
	MONSTER
	+FLOORCLIP
	states
	{
	Spawn:
		TIPZ A 10 A_Look
		loop
	See:
		THPZ A 0 A_JumpIfInventory("P50Clip",8,"Reload")
		THPZ AABBCCDD 4 A_Chase
		loop
	Missile:
		THPZ E 10 A_FaceTarget
		THPZ F 4 BRIGHT A_CustomBulletAttack(8,3,1,3)
		THPZ F 0 A_CustomMissile("WWPistolCasing",32,10,random(260,280))
		THPZ E 12 A_GiveInventory("P50Clip",1)
		goto See
	Reload:
		THPZ A 0 A_TakeInventory("P50Clip",8)
		THPZ E 8 A_PlaySound("colt45/clipout")
		THPZ E 8 A_CustomMissile("SpentClip1",32,10,random(260,280))
		THPZ D 16 A_PlaySound("colt45/clipin")
		goto See
	Pain:
		THPZ A 3
		THPZ A 3 A_Pain
		goto See
	Death.Lightning:
		LDTH A 2 BRIGHT A_PlaySound("misc/lightningdeath",5)
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
	Death:
		THPZ H 5
		THPZ I 5 A_Scream
		THPZ J 5 A_NoBlocking
		THPZ K 5
		THPZ L -1 A_PlaySound("misc/bodyfall")
		stop
	XDeath:
		THXZ A 0 A_SpawnDebris("NashGore_Gib9",0)
		THXZ A 0 A_SpawnDebris("NashGore_Gib10",0)
		THXZ A 0 A_SpawnDebris("NashGore_Gib11",0)
		THXZ A 0 A_SpawnDebris("NashGore_Gib11",0)
		THXZ A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		THXZ A 5 A_SpawnItemEx("WeaselPistolZombieArm", -10, 20, 0, 5, 5, 5, 150)
		THXZ B 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		THXZ B 5 A_XScream
		THXZ C 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		THXZ C 5 A_NoBlocking
		THXZ D 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		THXZ DE 5
		THXZ F -1
		stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		stop
	Death.Fire:
		BURN ABC 5 BRIGHT
		BURN D 5 BRIGHT A_PlaySound("misc/flamedeath")
		BURN EFGHIJKLMN 5 BRIGHT
		BURN O 5 BRIGHT A_NoBlocking
		BURN PQRSTU 5 BRIGHT
		BURN V -1
		Stop
	Death.Neut:
		DISR A 3 BRIGHT A_SetTranslucent(0.9,0)
		DISR B 3 BRIGHT A_PlaySound("misc/disintdeath")
		DISR C 3 BRIGHT A_SetTranslucent(0.8,0)
		DISR D 3 BRIGHT A_SetTranslucent(0.7,0)
		DISR E 3 BRIGHT A_SetTranslucent(0.6,0)
		DISR F 3 BRIGHT A_SetTranslucent(0.5,0)
		DISR G 3 BRIGHT A_SetTranslucent(0.4,0)
		DISR H 3 BRIGHT A_SetTranslucent(0.3,0)
		DISR I 3 BRIGHT A_SetTranslucent(0.2,0)
		DISR J 3 BRIGHT A_SetTranslucent(0.1,0)
		Stop
	Raise:
		THPZ KJIH 5
		goto See
	}
}

ACTOR WeaselPistolZombieArm
{
	Radius 6
	Height 6
	Speed 7
	BounceType Doom //Not sure if this is needed
	+DROPOFF
	+Missile
	States
	{
	Spawn:
		TARM AB 2
		Loop
	Death:
		TARM C -1
		Loop
	}
}