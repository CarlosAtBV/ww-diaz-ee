ACTOR ATAmmo : Ammo 8413
{
	Inventory.Amount 1
	Inventory.MaxAmount 5
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 10
	Inventory.Icon ATGAA0
	Inventory.PickupMessage "15.4mm AT30 ammo"
	Tag "HE Slugs"
	Inventory.PickupSound "weapons/slugout"
	States
	{
	Spawn:
		ATGA A -1
		Stop
	}
}

ACTOR ATClip : Ammo
{
	Inventory.MaxAmount 4
	Inventory.Icon ATGPA0
}

ACTOR SlugRifle : Weapon 8403
{
	Inventory.PickupMessage "k&s at30 slugthrower (5)"
	Tag "K&S AT30"
	Obituary "%o was mistaken for a heavy vehicle by %k."
	Inventory.PickupSound "weapons/slugslap"
	Weapon.AmmoType		"ATClip"
	Weapon.AmmoGive		0
	Weapon.AmmoUse		1
	Weapon.AmmoType2	"ATAmmo"
	Weapon.AmmoGive2	2
	Weapon.AmmoUse2		0
	Weapon.SisterWeapon SlugRifle2
	Radius				12
	Height				12
	+AMMO_OPTIONAL
	+NOALERT
	+NOAUTOFIRE
	States
	{
	Spawn:
		ATGP A -1
		Stop
	Select:
		TNT1 A 12 SetPlayerProperty(0,1,2)
		ATGH E 1 A_PlaySound("weapon/select6") 
		ATGH DCBA 1
    	ATGG A 1 A_Raise
    	Goto Select+6
    Deselect:
		ATGH A 1 A_PlaySound("weapon/deselect")
		ATGH BCDE 1
		TNT1 A 12
    	TNT1 A 1 A_Lower
    	Goto Deselect+6
	Ready:
		ATGG A 0 A_JumpIfInventory("IsReloading",1,"Reload")
		ATGG A 1 A_WeaponReady
		Loop
	Fire:
		ATGG A 0 A_JumpIfNoAmmo("Dryfire")
		ATGG D 0 A_PlayWeaponSound("weapons/slugrifle")
		ATGG D 1 BRIGHT A_FireCustomMissile("AT30Slug",0,1,8,0)
		ATGG D 0 BRIGHT A_SetPitch(pitch - 7.0)//ACS_Execute(287,0,25,random(2,4),0)
		ATGG D 0 A_FireCustomMissile("ChainSmokeSpawner",0,0,6,8)
		ATGF A 1 BRIGHT A_GunFlash
		ATGF B 1 BRIGHT A_AlertMonsters
		ATGG BCD 1
		ATGG E 1 A_FireCustomMissile("DiazRifleCasingSpawner2",-3,0,0,0)
		ATGG A 3
		Goto Ready
	DryFire:
		ATGG E 1 A_PlayWeaponSound("mauser/empty")
		ATGG A 5
		TNT1 A 0 A_JumpIfInventory("EnableAutoReload",1,"Reload")
		Goto Ready
	Reload:
		ATGG A 0 A_JumpIfInventory("ATClip",4,2)
		ATGG A 0 A_JumpIfInventory("ATAmmo",1,2)
		ATGG A 1 A_TakeInventory("IsReloading",1)
		Goto Ready
		ATGR ABCD 2
		ATGR E 2 A_PlayWeaponSound("weapons/slugout")
		TNT1 A 0 A_TakeInventory("ATClip",4)
		ATGR F 2 A_FireCustomMissile("SpentClip3",3,0,5,0)
		ATGR GHHHHH 2
		ATGR H 2 A_PlayWeaponSound("weapons/slugin")
		ATGR HHHGF 2
		TNT1 A 0 A_TakeInventory("ATAmmo",1)
		TNT1 A 0 A_GiveInventory("ATClip",4)
		ATGR IJKLMNO 2
		ATGR P 2 A_PlayWeaponSound("weapons/slugslap")
		ATGR QRSTU 2
		Goto Ready
	Flash:
      	TNT1 A 0
      	TNT1 A 2 A_Light2
      	TNT1 A 2 A_Light1
      	TNT1 A 1 A_Light0
      	Stop
	Altfire:
		ATGH ABCD 1
		ATGH E 1 
		TNT1 A 4
		TNT1 A 3 A_PlayWeaponSound("melee/swing")
		ATGM A 1
		ATGM B 1 A_CustomPunch(8,0,0,"HitPuff")
		ATGM CDEF 1
		TNT1 A 5
		ATGH EDCBBAA 1
		Goto Ready
	}
}

ACTOR SlugRifle2 : SlugRifle
{
	Weapon.SisterWeapon SlugRifle
	Weapon.AmmoGive2 0
	+POWERED_UP
	Obituary "%o was mistaken for a large building by %k."
	States
	{
	Reload:
		ATGG A 0 A_JumpIfInventory("ATClip",3,2)
		ATGG A 0 A_JumpIfInventory("ATAmmo",1,2)
		ATGG A 1 A_TakeInventory("IsReloading",1)
		Goto Ready
		ATGR ABCD 1
		ATGR E 1 A_PlayWeaponSound("weapons/slugout")
		TNT1 A 0 A_TakeInventory("ATClip",3)
		ATGR F 1 A_FireCustomMissile("SpentClip3",3,0,5,0)
		ATGR GHHHHH 1
		ATGR H 1 A_PlayWeaponSound("weapons/slugin")
		ATGR HHHGF 1
		TNT1 A 0 A_TakeInventory("ATAmmo",1)
		TNT1 A 0 A_GiveInventory("ATClip",4)
		ATGR IJKLMNO 1
		ATGR P 1 A_PlayWeaponSound("weapons/slugslap")
		ATGR QRSTU 1
		Goto Ready
	Altfire:
		ATGH ABCD 1
		ATGH E 1 A_PlayWeaponSound("melee/swing")
		TNT1 A 7
		ATGM A 1
		ATGM B 1 A_CustomPunch(32,0,0,"HitPuff")
		ATGM CDEF 1
		TNT1 A 5
		ATGH EDCBBAA 1
		Goto Ready
	}
}