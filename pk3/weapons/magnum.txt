ACTOR ExtraMagnumAmmo : Ammo
{
	//$Category ammunition
	Inventory.MaxAmount 6
	Inventory.PickupMessage ".500 S&W Revolver Ammo"
	Tag ".500 S&W"
	Inventory.PickupSound "webammo"
	Inventory.Icon SLODA0
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 12
	Scale 0.5
	States
	{
	Spawn:
		SLOD A -1
		LOOP
	}
}

ACTOR MagnumClip : Ammo
{
	Inventory.MaxAmount 6
	Inventory.Icon REVPA0
}

ACTOR Marteba : Weapon
{
	//$Category weapons
	Inventory.PickupMessage "MarTeba m2042 Revolver (2)"
	Tag "MarTeba"
	Inventory.PickupSound "webpick"
	Obituary "%o out-triggered %k."
	Weapon.AmmoType "MagnumClip"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "ExtraMagnumAmmo"
	Weapon.AmmoGive2 2
	Weapon.AmmoUse2 1
	Radius 12
	Height 12
	scale 0.35
	Weapon.SisterWeapon Marteba2
	+AMMO_CHECKBOTH
	+NOEXTREMEDEATH
	+NOAUTOFIRE
	+NOALERT
	States
	{
	Spawn:
		REVP A -1
		Loop
	Deselect:
		MKRC A 1 A_PlaySound("weapon/deselect")
		MKRC BCDEF 1
		TNT1 A 12
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_Raise
		Loop
	Ready:
		TNT1 A 12
		MKRC FEDCB 1
		MKRC A 1 A_PlaySound("weapon/select1")
	Ready2:
		MKRG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		MKRG A 0 A_JumpIfInventory("MagnumClip",1,1)
		Goto DryFire
		MKRG B 2 A_PlayWeaponSound("webfire")
		MKRG A 2 A_AlertMonsters
		MKRF A 0 A_GunFlash
		MKRF A 1 BRIGHT A_FireBullets(2,3,-1,33,"BulletPuff",1)
		MKRF AA 1 BRIGHT A_SetPitch(pitch - 3.0)
		MKRF B 1 BRIGHT A_FireCustomMissile("PistolSmokeSpawner",0,0,6,8)
		MKRF C 1 A_SetPitch(pitch - 2.4)
		MKRF D 1 A_SetPitch(pitch + 1.8)
		MKRF E 1 A_SetPitch(pitch + 1.2)
		MKRF F 1 A_SetPitch(pitch + 1.2)
		MKRF F 1 A_SetPitch(pitch + 0.8)
		MKRF GGHHH 1
		MKRG A 3
		TNT1 A 0 A_CheckReload
		Goto Ready2
	Dryfire:
		MKRG B 2 A_PlaySound("webempty")
		MKRG A 2
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		Goto Ready2
	Reload:
		TNT1 A 0 A_JumpIfInventory("MagnumClip",0,2)
		TNT1 A 0 A_JumpIfInventory("ExtraMagnumAmmo",1,2)
		MKRG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
		MKRR ABC 1
		MKRL AB 1
		MKRL C 2 A_PlayWeaponSound("webopen")
		MKRL DE 3
		MKRL F 7 A_PlayWeaponSound("webeject")
		MKRL GGGGGG 0 A_FireCustomMissile("MagnumCasingSpawner",-3,0,0,0)
		MKRL G 5
		MKRL HI 3
		MKRL J 3 A_PlayWeaponSound("webins")
		MKRL KL 4
		TNT1 A 0 A_TakeInventory("ExtraMagnumAmmo",1)
		TNT1 A 0 A_GiveInventory("MagnumClip",6)
		MKRL M 3
		MKRL N 5
		MKRL O 5 A_PlayWeaponSound("webclose")
		MKRL PQ 2
		MKRL R 1 A_PlayWeaponSound("webspin")
		MKRG ABABABAABBAAABBB 1 A_WeaponReady
		Goto Ready2
	AltFire:
		MKRR AB 1
		MKRM A 2
		MKRM B 1 A_PlayWeaponSound("melee/swing")
		MKRM C 1 A_CustomPunch(5,0,0,"HitPuff")
		MKRM DE 1
		MKRM FGHI 2
		MKRG A 2
		Goto Ready2
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

ACTOR Marteba2 : Marteba
{
	Weapon.SisterWeapon Marteba
	Weapon.AmmoGive2 0
	+POWERED_UP
	States
	{
	Fire:
		MKRG A 0 A_JumpIfInventory("MagnumClip",1,1)
		Goto DryFire
		MKRG A 0 A_PlayWeaponSound("webfire")
		MKRG B 1
		MKRG A 1 A_AlertMonsters
		MKRF A 0 BRIGHT A_GunFlash
		MKRF A 1 BRIGHT A_FireBullets(2,3,-1,33,"BulletPuff",1)
		MKRF A 1 BRIGHT A_SetPitch(pitch - 2.0)//ACS_Execute(287,0,100,random(22,44),0)
		MKRF A 1 BRIGHT A_SetPitch(pitch - 2.0)//ACS_Execute(287,0,100,random(22,44),0)
		MKRF B 1 BRIGHT A_FireCustomMissile("PistolSmokeSpawner",0,0,6,8)
		MKRF C 1 A_SetPitch(pitch - 1.4)//ACS_Execute(287,0,20,random(6,11),0)
		MKRF D 1 A_SetPitch(pitch + 0.8)//ACS_Execute(287,0,10,random(3,7),0)
		MKRF E 1 A_SetPitch(pitch + 0.2)//ACS_Execute(287,0,-25,random(-22,-11),0)
		MKRF F 1 A_SetPitch(pitch + 0.2)//ACS_Execute(287,0,-50,random(-11,-5),0)
		MKRF F 1 A_SetPitch(pitch + 0.8)//ACS_Execute(287,0,-25,random(-11,-5),0)
		MKRF GHH 1
		MKRG A 2
		Goto Ready2
	Reload:
		TNT1 A 0 A_JumpIfInventory("MagnumClip",0,2)
		TNT1 A 0 A_JumpIfInventory("ExtraMagnumAmmo",1,2)
		MKRG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
		MKRR ABC 1
		MKRL AB 1
		MKRL C 1 A_PlayWeaponSound("webopen")
		MKRL DE 2
		MKRL F 4 A_PlayWeaponSound("webeject")
		MKRL GGGGGG 0 A_FireCustomMissile("MagnumCasingSpawner",-3,0,0,0)
		MKRL G 3
		MKRL HI 2
		MKRL J 2 A_PlayWeaponSound("webins")
		MKRL KL 2
		TNT1 A 0 A_TakeInventory("ExtraMagnumAmmo",1)
		TNT1 A 0 A_GiveInventory("MagnumClip",6)
		MKRL M 1
		MKRL N 3
		MKRL O 3 A_PlayWeaponSound("webclose")
		MKRL P 1
		Goto Ready2
	AltFire:
		MKRR AB 2
		MKRM A 2 A_PlayWeaponSound("melee/swing")
		MKRM B 1
		MKRM C 1 A_CustomPunch(20,0,0,"PowerHitPuff")
		MKRM DE 1
		MKRM FGHI 2
		MKRG A 2
		Goto Ready2
	}
}