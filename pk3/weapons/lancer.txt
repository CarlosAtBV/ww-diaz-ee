ACTOR LanceDriver : Weapon
{
	Inventory.PickupMessage "Superheated Magnetic Lance Driver (7)"
	Tag "Lance Driver"
	Inventory.PickupSound "lance/deselect"
	Obituary "%o couldn't stand in the way of %k's lance driver."
	Weapon.AmmoType "NeutClip"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "NeutAmmo"
	Weapon.AmmoGive2 2
	Weapon.AmmoUse2 1
	Radius 12
	Height 12
	Weapon.Kickback 250
	Scale 0.25
	+AMMO_CHECKBOTH
	+NOAUTOFIRE
	States
	{
	Spawn:
		LNDP A -1
		Loop
	Deselect:
		LNDH A 1 A_PlayWeaponSound("lance/deselect")
		LNDH BCD 1
		TNT1 A 10
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_Raise
		Loop
	Ready:
		TNT1 A 15
		LNDH D 1 A_PlayWeaponSound("lance/select")
		LNDH CB 1
	Ready2:
		TNT1 A 0 A_TakeInventory("PowerLevel")
		TNT1 A 0 A_TakeInventory("LevelCounter")
		LNDG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("NeutClip",1,1)
		Goto DryFire
	    // TNT1 A 0 A_JumpIfInventory("Overload",1,"Hyperfire")  //for planned rapid fire overdrive mode
		TNT1 A 0 A_PlayWeaponSound("lance/charge")
	Hold://	LevelCheck:
		TNT1 A 0 A_GiveInventory("LevelCounter")
		TNT1 A 0 A_GiveInventory("FrameIdle")  //increment the frame duration
	    TNT1 A 0 A_JumpIfInventory("LevelCounter",0,1)
	    Goto Level1Charge
		TNT1 A 0 A_TakeInventory("LevelCounter")  //what 9000!
		TNT1 A 0 A_PlayWeaponSound("lance/charge")
		TNT1 A 0 A_GiveInventory("PowerLevel")
	Level1Charge:
		TNT1 A 0 A_JumpIfInventory("PowerLevel",1,"Level2Charge")
		TNT1 A 0 A_JumpIfInventory("FrameIdle",0,1)  //check frame duration
		Goto Display
		TNT1 A 0 A_TakeInventory("FrameIdle")
		Goto FrameCheck
	Level2Charge:
		TNT1 A 0 A_JumpIfInventory("PowerLevel",2,"Level3Charge")
		TNT1 A 0 A_PlaySound("lance/level1",CHAN_WEAPON,1.0,1)
		TNT1 A 0 A_JumpIfInventory("FrameIdle",5,1)
		Goto Display
		TNT1 A 0 A_TakeInventory("FrameIdle")
		Goto FrameCheck
	Level3Charge:
		TNT1 A 0 A_JumpIfInventory("PowerLevel",3,"Level4Charge")
		TNT1 A 0 A_PlaySound("lance/level2",CHAN_WEAPON,1.0,1)
		TNT1 A 0 A_JumpIfInventory("FrameIdle",3,1)
		Goto Display
		TNT1 A 0 A_TakeInventory("FrameIdle")
		Goto FrameCheck
	Level4Charge:
		TNT1 A 0 A_PlaySound("lance/level3",CHAN_WEAPON,1.0,1)
		TNT1 A 0 A_JumpIfInventory("FrameIdle",1,1)
		Goto Display
		TNT1 A 0 A_TakeInventory("FrameIdle")
	FrameCheck:
		TNT1 A 0 A_GiveInventory("FrameCounter",1)
	    TNT1 A 0 A_JumpIfInventory("FrameCounter",0,1) //check frame letter
		Goto Display
		TNT1 A 0 A_TakeInventory("FrameCounter")
		Goto Display
	Display:
		LNDF A 1 BRIGHT A_JumpIfInventory("FrameCounter",1,2)
		TNT1 A 0 A_Refire
		Goto AttackCheck
		LNDF B 1 BRIGHT A_JumpIfInventory("FrameCounter",2,2)
		TNT1 A 0 A_Refire
		Goto AttackCheck
		LNDF C 1 BRIGHT
		TNT1 A 0 A_Refire
		Goto AttackCheck
	AttackCheck:
	    TNT1 A 0 A_JumpIfInventory("PowerLevel",1,"Level2")
	Level1:
		LNDG A 0 // A_PlayWeaponSound("lance/failed")
		Goto Dryfire  //not charged enough to attack with
	Level2:
	    TNT1 A 0 A_JumpIfInventory("PowerLevel",2,"Level3")
		//needs a real attack
		TNT1 A 0 A_PlayWeaponSound("lance/fire")
		TNT1 A 0 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,3,6,0)
		TNT1 A 0 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,6,9,0)
		TNT1 A 0 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,9,6,0)
		TNT1 A 0 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,4,4,0)
		TNT1 A 0 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,4,8,0)
		TNT1 A 0 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,8,8,0)
		LNDF A 1 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),1,8,4,0)
		LNDF BCDEFG 1 BRIGHT
		LNDG FEBA 2
		LNDG A 5 A_PlayWeaponSound("lance/Ready2")
		TNT1 A 0 A_CheckReload
		Goto Ready2
	Level3:
	    TNT1 A 0 A_JumpIfInventory("PowerLevel",3,"Level4")
		LNDG A 0 A_JumpIfInventory("NeutClip",6,1)
		Goto DryFire
		LNDF A 0 A_PlayWeaponSound("lance/fire")
		LNDF A 0 BRIGHT //ACS_Execute(287,0,180,random(15,35),0)
		LNDF A 0 BRIGHT A_RailAttack(200,6,1,"none","A0 FF 99",1,3)
		LNDF A 0 BRIGHT A_FireCustomMissile("DriverLance2",0,1,6,6,0)
		LNDF A 0 BRIGHT A_FireCustomMissile("DriverLance3",Random (-3, 3),1,6,16,0)
		LNDF A 0 BRIGHT A_FireCustomMissile("DriverLance3",Random (-3, 3),1,6,-2,0)
		LNDF A 0 BRIGHT A_FireCustomMissile("DriverLance3",Random (-3, 3),1,16,6,0)
		LNDF A 0 BRIGHT A_FireCustomMissile("DriverLance3",Random (-3, 3),1,-2,6,0)
		LNDF A 1 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,3,6,0)
		LNDF B 1 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,6,9,0)
		LNDF C 1 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,9,6,0)
		LNDF D 1 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,4,4,0)
		LNDF E 1 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,4,8,0)
		LNDF F 1 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,8,8,0)
		LNDF G 1 BRIGHT A_FireCustomMissile("DriverLance",Random (-7, 7),0,8,4,0)
		LNDG FEBA 2
		TNT1 A 0 A_CheckReload
		Goto Ready2
	Level4:  //OVERDRIVE!!!!
		LDNF A 0 A_PlaySound("lance/exploburst",CHAN_WEAPON)
		LDNF A 0 A_SetBlend("A0 FF 99", 0.95, 70)
		LDNF AAAAAAAAAAAAAAAAAAAAAAAA 0 A_FireBullets(0,0,0,0,"MechaBallSpark",1)
		LDNF A 1 BRIGHT A_SpawnItemEx("DriverBlast",0,0,32)
		LNDF BCDEFG 1 BRIGHT
		LNDG FEBA 2
		TNT1 A 0 A_CheckReload
		Goto Ready2
	HyperFire:
	    //add attacks for rapid fire overload mode here
		Goto Ready2
	DryFire:
		LNDG A 5 A_PlayWeaponSound("lance/failed")
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		Goto Ready2
	Reload:
		LNDG A 0 A_JumpIfInventory("NeutClip",24,2)
		LNDG A 0 A_JumpIfInventory("NeutAmmo",6,2)
		LNDG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
		LNDR A 3 A_PlayWeaponSound("skorpion/clipout")
		LNDR B 3 A_SpawnItemEx("SpentClip5",random(-6,6),3,5)
		LNDR CCCCC 3 A_SpawnItemEx("SpentClip5",random(-6,6),3,5)
		LNDR C 0 A_PlayWeaponSound("lance/reload")
		LNDR DEFGCCCC 3
		LNDR B 4
		TNT1 A 0 A_TakeInventory("NeutAmmo",6)
		TNT1 A 0 A_GiveInventory("NeutClip",24)
		LNDR A 4
		Goto Ready2
	}
}

ACTOR NeutClip : Ammo
{
	Inventory.MaxAmount 24
	Inventory.Icon LNDPA0
}

ACTOR FrameCounter : Inventory
{
    Inventory.MaxAmount 3
}

ACTOR FrameIdle : Inventory
{
    Inventory.MaxAmount 7
}

ACTOR PowerLevel : Inventory
{
    Inventory.MaxAmount 3
}

ACTOR LevelCounter : Inventory
{
    Inventory.MaxAmount 45
}

ACTOR DriverLance
{
	Scale 0.4
	Speed 60
	Damage 10
	RenderStyle Add
	Alpha 0.7
	PROJECTILE
	DeathSound "lance/explode"
	Obituary "%o is another victim of %k's magnetic personality."
	+RIPPER
	+STRIFEDAMAGE
	-NOTELEPORT
	Decal BFGLightningB
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_PlaySound("lance/fly",CHAN_WEAPON,1.0,1)
		TNT1 A 0 A_Jump(128,2)
		LNDS AB -1
		Loop
	Death:
		LNDS BBBBBB 0 A_SpawnDebris("MechaBallSpark")
		LNDS B 1 A_StopSound(CHAN_WEAPON)
		Stop
	}
}

ACTOR DriverLance3 : DriverLance
{
    States
	{
	Death:
		LNDS B 0 A_BFGSpray("DummyBFGSpray",0,2)
		LNDS BBBBBB 0 A_SpawnDebris("MechaBallSpark")
		LNDS B 1
		Stop
	}
}

ACTOR DriverLance2 : DriverLance
{
    DeathSound "lance/explode"
    Decal BFGLightning
    States
	{
	Spawn2:
		LNDS B 1 Radius_Quake(4,10,0,10,0)
	}
}

ACTOR DriverBlast
{
	Obituary "%o receives a Tan from Hell, thanks to %k's magnetic blast."
	Projectile
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 AA 1 A_Explode(500,1024,0)
		Stop
	}
}

ACTOR DiazBFGSpray replaces BFGExtra
{
	Scale 0.4
	RenderStyle Add
	Alpha 0.7
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
	Spawn:
		LNDS B 0
		LNDS BB 0 A_SpawnDebris("MechaBallSpark")
		LNDS B 1
		Stop
	}
}

Actor DummyBFGSpray
{
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	}
}