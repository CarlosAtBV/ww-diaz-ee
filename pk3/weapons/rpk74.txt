ACTOR RPKClip : Ammo
{
	Inventory.MaxAmount 100
	Inventory.Icon RPDPA0
}

ACTOR RPKSetup : Inventory{}

ACTOR RPKAmmo : Ammo replaces Cell
{
	//$Category ammunition
	Inventory.MaxAmount 2
	Inventory.PickupMessage "5.45mm RPK Ammo Drum"
	Tag "5.45x39mm"
	Inventory.PickupSound "rpd/ammo"
	Ammo.BackpackMaxAmount 4
	Scale 0.25
	States
	{
	Spawn:
		RPDA A -1
		LOOP
	}
}

ACTOR RPK : Weapon replaces PlasmaRifle
{
	Inventory.PickupMessage "ruchnoy pulemyot kalashnikova (rpk-74) (5)"
	Tag "RPK-74"
	Inventory.PickupSound "rpd/pick"
	Obituary "%o was machine-gunned Russian-style by %k."
	Weapon.AmmoType "RPKClip"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "RPKAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 1
	Weapon.SisterWeapon RPK2
	Radius 12
	Height 12
	+AMMO_CHECKBOTH
	+NOEXTREMEDEATH
	+NOALERT
	+NOAUTOFIRE
	scale 0.75
	States
	{
	Spawn:
		RPDP A -1
		LOOP
	Deselect:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDD A 0 A_JumpIfInventory("RPKSetup",1,"DeselectSetup")
		RPDH A 1 A_PlaySound("weapon/deselect")
		RPDH BC 1
		TNT1 A 19 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		TNT1 A 0 A_Lower
		Wait
	DeselectSetup:
		RPDC J 3
		RPDC I 3 A_PlaySound("rpd/setup")
		RPDC HGFEDC 3
		RPDC B 3 SetPlayerProperty(0,0,PROP_FROZEN)
		RPDC A 3 A_TakeInventory("RPKSetup")
		Goto Deselect
	Select:
		TNT1 A 0 A_Raise
		Loop
	Ready:
		TNT1 A 19
		RPDH C 1
		RPDH B 1 A_PlaySound("weapon/select2")
		RPDH A 1
	Ready2:
		RPDD A 0 A_JumpIfInventory("RPKSetup",1,2)
		RPDD A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
		RPDG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		RPDD A 0 A_JumpIfInventory("RPKSetup",1,"ReadyFire")
		RPDC ABCD 3
		RPDU A 2 ACS_NamedExecute("PlayerSpeed",0,50,0,0)
		RPDU B 5
		RPDU B 1 A_Refire
		RPDU A 2 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		RPDC DCBA 2 A_WeaponReady
		Goto Ready2
	Hold:
		RPDU B 0 A_JumpIfInventory("RPKSetup",1,"ReadyFire")
		RPDU B 0 A_JumpIfInventory("RPKClip",1,1)
		Goto DryFireHold
		RPDU B 0 A_PlaySound("rpd/fire",CHAN_WEAPON,1.0,1)
		RPF2 B 0 A_AlertMonsters
		RPF2 B 0 BRIGHT A_GunFlash
		RPF2 B 0 BRIGHT A_SetPitch(pitch - 4.0)//ACS_Execute(287,0,50,random(2,4),0)
		RPF2 A 1 Bright A_FireBullets(4,3,-1,10,"BulletPuff",1)
		RPF2 B 0 A_FireCustomMissile("DiazRifleCasingSpawner",-3,0,0,0)
		RPF2 B 1 Bright A_FireCustomMissile("ChainSmokeSpawner",0,0,6,8)
		RPF2 B 0 BRIGHT A_GunFlash
		RPF2 B 0 BRIGHT A_SetPitch(pitch - 4.0)//ACS_Execute(287,0,50,random(2,4),0)
		RPF2 C 1 Bright A_FireBullets(4,3,-1,10,"BulletPuff",1)
		RPF2 D 0 A_FireCustomMissile("DiazRifleCasingSpawner",-3,0,0,0)
		RPF2 D 1 Bright A_FireCustomMissile("ChainSmokeSpawner",0,0,6,8)
		RPF2 D 0 A_Refire
		RPDU B 0 A_StopSound(CHAN_WEAPON)
		RPDU B 0 A_PlayWeaponSound("rpd/decay")
		RPDU A 2 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		RPDC DCBA 2
		Goto Ready2
	ReadyFire:
		RPDG A 0 A_JumpIfInventory("RPKClip",1,1)
		Goto ReadyDryFire
		RPDG A 0 ACS_NamedExecute("PlayerSpeed",0,20,0,0)
		RPDG A 0 A_PlaySound("rpd/fire",CHAN_WEAPON,1.0,1)
		RPDF B 0 A_AlertMonsters
		RPDF B 0 BRIGHT A_GunFlash
		RPF2 B 0 BRIGHT A_SetPitch(pitch - 1.5)//ACS_Execute(287,0,25,random(2,4),0)
		RPDF A 1 Bright A_FireBullets(2,1,-1,10,"BulletPuff",1)
		RPDF B 0 A_FireCustomMissile("DiazRifleCasingSpawner",-3,0,0,0)
		RPDF B 1 Bright A_FireCustomMissile("ChainSmokeSpawner",0,0,6,8)
		RPDF B 0 BRIGHT A_GunFlash
		RPF2 B 0 BRIGHT A_SetPitch(pitch - 1.5)//ACS_Execute(287,0,25,random(2,4),0)
		RPDF C 1 Bright A_FireBullets(2,1,-1,10,"BulletPuff",1)
		RPDF D 0 A_FireCustomMissile("DiazRifleCasingSpawner",-3,0,0,0)
		RPDF D 1 Bright A_FireCustomMissile("ChainSmokeSpawner",0,0,6,8)
		RPDF D 0 A_Refire
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDG A 0 ACS_NamedExecute("PlayerSpeed",0,50,0,0)
		RPDG A 0 A_PlayWeaponSound("rpd/decay")
		Goto Ready2
	DryFire:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDC ABCD 3
		RPDU A 2
		RPDU B 5
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		RPDU B 1 A_Refire
	DryFireHold:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDU B 2
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"HoldReload")
		RPDU B 1 A_Refire
		RPDU B 2 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		RPDC DCBA 2
		Goto Ready2
	ReadyDryFire:
		RPDG A 2 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"ReadyReload")
		RPDG A 1 A_PlaySound("skorpion/noammo")
		RPDG A 0 ACS_NamedExecute("PlayerSpeed",0,50,0,0)
		Goto Ready2
	HoldReload:
		TNT1 A 0 A_JumpIfInventory("RPKClip",100,"HoldReloadNevermind")
		TNT1 A 0 A_JumpIfInventory("RPKAmmo",1,"HoldReloadStart")
	HoldReloadNevermind:
		RPDC DCBA 2
	  	Goto Ready2
	HoldReloadStart:
		RPDU B 0 A_StopSound(CHAN_WEAPON)
		RPDC ABCD 3
	Reload:
		"####" A 0 A_JumpIfInventory("RPKClip",100,2)
		"####" A 0 A_JumpIfInventory("RPKAmmo",1,2)
		"####" A 1 A_WeaponReady(WRF_NOFIRE)
	  	Goto Ready2
		RPDC A 0 A_JumpIfInventory("RPKSetup",1,"ReadyReload")
		RPDU B 0 A_StopSound(CHAN_WEAPON)
		RPDC ABCD 3
	ReloadWorking:
		RPDU A 2
		RPDU B 5
		RPR2 A 3 A_PlaySound("rpd/load")
		RPR2 B 3
		RPR2 C 12
		RPR2 DE 3
		RPR2 D 8
		RPR2 F 0 A_TakeInventory("RPKAmmo",1)
		RPR2 F 3 A_GiveInventory("RPKClip",100)
		RPR2 GF 3
		RPR2 D 18
		RPR2 IJ 2
		RPR2 K 10
		RPR2 L 2
		RPDU B 1
		RPDU A 2
		RPDC DCB 2
		RPDC A 2 A_Refire
		Goto Ready2
	ReadyReload:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDR A 3 A_PlaySound("rpd/load")
		RPDR B 3
		RPDR C 12
		RPDR DE 3
		RPDR D 8
		RPDR F 0 A_TakeInventory("RPKAmmo",1)
		RPDR F 3 A_GiveInventory("RPKClip",100)
		RPDR GF 3
		RPDR D 18
		RPDR IJ 2
		RPDR K 10
		RPDR L 2
		RPDG A 1 A_Refire
		Goto Ready2
	Altfire:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDC A 0 A_JumpIfInventory("RPKSetup",1,"AltFireSetup")
		RPDC A 2 A_PlaySound("rpd/setup")
		RPDC B 3 ACS_NamedExecute("PlayerSpeed",0,50,0,0)
		RPDC CDEFGHIJ 3
		RPDG A 4 A_GiveInventory("RPKSetup")
		Goto Ready2
	AltFireSetup:
		RPDC J 3
		RPDC I 3 A_PlaySound("rpd/setup")
		RPDC HGFEDC 3
		RPDC B 3 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		RPDC A 3 A_TakeInventory("RPKSetup")
		Goto Ready2
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

ACTOR RPK2 : RPK
{
	Weapon.SisterWeapon RPK
	Weapon.AmmoGive2 0
	+POWERED_UP
	Obituary "There is nothing left of %o, now that %k is done with %h."
	States
	{
	Deselect:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDD A 0 A_JumpIfInventory("RPKSetup",1,"DeselectSetup")
		RPDH A 1 A_PlaySound("weapon/deselect")
		RPDH BC 1
		TNT1 A 10 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		TNT1 A 0 A_Lower
		Wait
	DeselectSetup:
		RPDC J 2
		RPDC I 2 A_PlaySound("rpd/setup")
		RPDC HGFEDC 2
		RPDC B 2 SetPlayerProperty(0,0,PROP_FROZEN)
		RPDC A 2 A_TakeInventory("RPKSetup")
		Goto Deselect
	Fire:
		RPDD A 0 A_JumpIfInventory("RPKSetup",1,"ReadyFire")
		RPDC ABCD 1
		RPDU A 1 ACS_NamedExecute("PlayerSpeed",0,50,0,0)
		RPDU B 3
		RPDU B 1 A_Refire
		TNT1 A 0 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		Goto Ready2
	Hold:
		RPDU B 0 A_JumpIfInventory("RPKSetup",1,"ReadyFire")
		RPDU B 0 A_JumpIfInventory("RPKClip",1,1)
		Goto DryFireHold
		RPDU B 0 A_PlaySound("rpd/fire",CHAN_WEAPON,1.0,1)
		RPF2 B 0 A_AlertMonsters
		RPF2 B 0 BRIGHT A_GunFlash
		RPF2 B 0 BRIGHT A_SetPitch(pitch - 4.0)//ACS_Execute(287,0,50,random(2,4),0)
		RPF2 A 1 Bright A_FireBullets(4,3,-1,10,"BulletPuff",1)
		RPF2 B 0 A_FireCustomMissile("DiazRifleCasingSpawner",-3,0,0,0)
		RPF2 B 1 Bright A_FireCustomMissile("ChainSmokeSpawner",0,0,6,8)
		RPF2 B 0 BRIGHT A_GunFlash
		RPF2 B 0 BRIGHT A_SetPitch(pitch - 4.0)//ACS_Execute(287,0,50,random(2,4),0)
		RPF2 C 1 Bright A_FireBullets(4,3,-1,10,"BulletPuff",1)
		RPF2 D 0 A_FireCustomMissile("DiazRifleCasingSpawner",-3,0,0,0)
		RPF2 D 1 Bright A_FireCustomMissile("ChainSmokeSpawner",0,0,6,8)
		RPF2 D 0 A_Refire
		RPDU B 0 A_StopSound(CHAN_WEAPON)
		RPDU B 0 A_PlayWeaponSound("rpd/decay")
		RPDU A 2 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		RPDC DCBA 1
		Goto Ready2
	DryFireHold:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDU B 2
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"HoldReload")
		RPDU B 1 A_Refire
		RPDU B 1 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		RPDC DCBA 1
		Goto Ready2
	Reload:
		TNT1 A 0 A_JumpIfInventory("RPKClip",100,2)
		TNT1 A 0 A_JumpIfInventory("RPKAmmo",1,2)
		"####" A 1 A_WeaponReady(WRF_NOFIRE)
	  	Goto Ready2
		RPDC A 0 A_JumpIfInventory("RPKSetup",1,"ReadyReload")
		RPDU B 0 A_StopSound(CHAN_WEAPON)
		RPDC ABCD 2
	ReloadWorking:
		RPDU A 1
		RPDU B 3
		RPR2 A 2 A_PlaySound("rpd/load")
		RPR2 B 2
		RPR2 C 8
		RPR2 D 2
		RPR2 E 2
		RPR2 D 6
		RPR2 F 0 A_TakeInventory("RPKAmmo",1)
		RPR2 F 2 A_GiveInventory("RPKClip",100)
		RPR2 GF 2
		RPR2 D 9
		RPR2 IJ 1
		RPR2 K 7
		RPR2 L 1
		RPDU B 1
		RPDU A 1
		RPDC DCB 1
		RPDC A 1 A_Refire
		Goto Ready2
	ReadyReload:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDR A 2 A_PlaySound("rpd/load")
		RPDR B 2
		RPDR C 8
		RPDR D 2
		RPDR E 2
		RPDR D 6
		RPDR F 0 A_TakeInventory("RPKAmmo",1)
		RPDR F 2 A_GiveInventory("RPKClip",100)
		RPDR GF 2
		RPDR D 9
		RPDR IJ 1
		RPDR K 7
		RPDR L 1
		RPDG A 1 A_Refire
		Goto Ready2
	Altfire:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDC A 0 A_JumpIfInventory("RPKSetup",1,"AltFireSetup")
		RPDC A 1 A_PlaySound("rpd/setup")
		RPDC B 2 ACS_NamedExecute("PlayerSpeed",0,50,0,0)
		RPDC CDEFGHIJ 2
		RPDG A 3 A_GiveInventory("RPKSetup")
		Goto Ready2
	AltFireSetup:
		RPDG A 0 A_StopSound(CHAN_WEAPON)
		RPDC J 2
		RPDC I 2 A_PlaySound("rpd/setup")
		RPDC HGFEDC 2
		RPDC B 2 ACS_NamedExecute("PlayerSpeed",0,100,0,0)
		RPDC A 2 A_TakeInventory("RPKSetup")
		Goto Ready2
	}
}