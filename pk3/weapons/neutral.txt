ACTOR NeutAmmo : Ammo replaces CellPack
{
	Inventory.PickupMessage "Compressed Energy Cell"
	Tag "Energy Cells"
	Inventory.PickupSound "neutralizer/ammo"
	Inventory.Icon NEUAA0
	Inventory.Amount 2
	Inventory.MaxAmount 12
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 24
	Scale 0.75
	States
	{
	Spawn:
		NEUA A -1
		Loop
	}
}

ACTOR Neutralizer : Weapon replaces BFG9000
{
	Inventory.PickupMessage "Compressed Energy Neutralizer (7)"
	Inventory.PickupSound "neutralizer/pickup"
	Obituary "%o was turned into toothpaste by %k."
	Weapon.AmmoType		"NeutAmmo"
	Weapon.AmmoGive		2
	Weapon.AmmoUse		1
	Radius				12
	Height				12
	Weapon.Kickback		250
	+NOAUTOFIRE
	States
	{
	Spawn:
		NEUP A -1
		Loop
	Select:
		TNT1 A 10 SetPlayerProperty(0,1,2)
		NEUH E 1 A_PlayWeaponSound("neutralizer/pickup")
		NEUH DCBA 1
		NEUT A 1 A_Raise
		Goto Select+6
	Deselect:
		NEUH A 1 A_PlayWeaponSound("neutralizer/down")
		NEUH BCDE 1
		TNT1 A 10
		TNT1 A 1 A_Lower
		Goto Deselect+6
	Ready:
		NEUT AAAAABBBBBCCCCCBBBBB 1 A_WeaponReady
		Loop
	Fire:
		NEUC ABCDEFG 1
		NEUT A 0 A_PlayWeaponSound("neutralizer/fire")
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",12,0,15,5)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",-12,0,15,5)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",6,0,15,5)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",-6,0,15,5)
		NEUF A 2 BRIGHT A_FireCustomMissile("SwarmBolt",0,0,15,5)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",12,0,15,0)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",-12,0,15,0)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",6,0,15,0)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",-6,0,15,0)
		NEUF A 2 BRIGHT A_FireCustomMissile("SwarmBolt",0,0,15,0)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",12,0,15,-5)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",-12,0,15,-5)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",6,0,15,-5)
		NEUT B 0 BRIGHT A_FireCustomMissile("SwarmBolt",-6,0,15,-5)
		NEUF A 2 BRIGHT A_FireCustomMissile("SwarmBolt",0,1,15,-5)
		NEUF BC 2 BRIGHT
		NEUI A 3 A_PlaySoundEx("neutralizer/charge","SoundSlot5",0)
		NEUI BCDEFGH 3
		Goto Ready
	}
}

