//===========================================================================
//
// Diaz's Melee Style
//
// by Cardboard Marty
// Originally adapted from ww-diaz-v3 by wildweasel
// Sprites by Cardboard Marty and wildweasel
// Bases by Blox, Scuba Steve, Banjo Entertainment
//
//
//===========================================================================

Class Weapon_Diaz_Melee : DiazEE_Weapon_Core
{
	Default
	{
		Inventory.PickupMessage "REPLACE THIS";
		Obituary "%o was taken out by %k's Muay Thai barrage.";
		Weapon.Kickback 100;
		Weapon.BobStyle "Alpha";
		Weapon.BobSpeed 1.5;
		Weapon.BobRangeY 1.0;
		Weapon.BobRangeX 0.5;
		Tag "Unarmed";
		
		+WEAPON.WIMPY_WEAPON
		+WEAPON.MELEEWEAPON
		+WEAPON.NOALERT
		+WEAPON.NOAUTOAIM
		+WEAPON.NOAUTOFIRE
		+NOEXTREMEDEATH
		+INVENTORY.UNDROPPABLE
	}

	int combo_mach;
	int combo_alt;
	int combo_primary;
	
	int combo_held;

	States
	{
		Spawn:
			TNT1 A -1;
			Loop;
		Deselect:
			TNT1 A 0 A_TakeInventory("MeleeSelected", 1);
			"----" A 0
			{
				invoker.combo_primary = 0;
				invoker.combo_alt = 0;
				invoker.combo_mach = 0;
			}
			TNT1 A 0 A_JumpIfInventory("ComboCoolDown", 1, "ComboDeselect");
			TNT1 A 0 A_TakeInventory("ComboCoolDown", 99);
			MART VW 1;
			TNT1 A 4;
		DeselectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower;
			TNT1 A 1 A_Lower;
			Loop;
		ComboDeselect:
			TNT1 A 0 A_Lower;
			Loop;
		Select:
			TNT1 A 0 A_GiveInventory("MeleeSelected", 1);
			TNT1 A 0 A_WeaponOffset(0,32);
			TNT1 A 1;
			MART WV 1;
			MART A 1 A_Raise;
			Loop;
		Ready:
			TNT1 A 0 A_JumpIfInventory("TauntPressed",1,"Taunt");
			MART A 1 A_WeaponReady;
			Loop;
		Taunt:
			MART WV 1;
			TNT1 A 0 A_StartSound("*taunt", CHAN_VOICE);
			CMON ABC 1;
			CMON C 6 A_AlertMonsters;
			CMON DEFFFEDCCCEFFFEDCCCCCCBA 1;
			MART WV 1;
			Goto Ready;
			
	// Punch!
		Fire:
			"----" A 0
			{
				switch(invoker.combo_primary)
				{
					case 2: return resolveState("CrossFinish"); break; 		//P-P-P
					case 1: return resolveState("LeftJab"); break; 		//P-P
					case 0: break;
				}
				
				//Just go to punch lmao
				return resolveState("Punch1");
			}
			Stop;

		// P
		Punch1:
			MART A 0 A_StartSound("basicmelee/miss", CHAN_AUTO);
			MART BC 1;
			MART D 1 A_DiazEE_Melee(20, 70, "BasicMeleePuff");
			"----" A 0
			{
				invoker.combo_primary++;
			}
			TNT1 A 0 A_GiveInventory("ComboCoolDown", 1);
			MART EDCBAAA 1 A_WeaponReady;
			Goto AfterMelee;
		
		// P-P
		LeftJab:
			MART I 1 A_StartSound("basicmelee/miss", CHAN_AUTO);
			MART J 1;
			MART K 1 A_DiazEE_Melee(20, 70, "BasicMeleePuff");
			"----" A 0
			{
				invoker.combo_primary++;
			}
			TNT1 A 0 A_GiveInventory("ComboCoolDown", 1);
			MART LMNOPPAAA 1 A_WeaponReady;
			Goto AfterMelee;
		
		// P-P-P
		CrossFinish:
			MART VW 1;
			TNT1 A 3;
			TNT1 A 0 A_StartSound("strongmelee/miss", CHAN_AUTO);
			MAR3 ABCD 1;
			MAR3 E 1 A_DiazEE_Melee(30, 70, "StrongMeleePuff");
			"----" A 0
			{
				invoker.combo_primary++;
			}
			MAR3 FGHIJK 1 A_WeaponReady(WRF_NOPRIMARY);
			MART WVA 1;
			Goto AfterMelee;

		AltFire:
			"----" A 0
			{
				switch(invoker.combo_primary)
				{
					case 3: return resolveState("MapBreaker"); break; 				//P-P-P-K
					case 2: return resolveState("Roundhouse"); break; 				//P-P-K
					case 1: return resolveState("RightStraight"); break;			//P-K
				}
				
				//Just go to kick lmao
				return resolveState("DefaultKick");
			}
			Stop;
			
	// K Default
		DefaultKick:
			MART Q 2 SetPlayerProperty(0, 1, PROP_FROZEN);
			MART R 1 A_StartSound("strongmelee/miss", CHAN_AUTO);
			MART US 1;
			MART T 4 A_DiazEE_Melee(30, 90, "StrongMeleePuff");
			MART SU 2;
			TNT1 A 4 SetPlayerProperty(0, 0, PROP_FROZEN);
			MART WV 1;
			MART A 8;
			Goto AfterMelee;
			
	// P-K
		RightStraight:
			TNT1 A 5 A_StartSound("*largemelee", CHAN_VOICE);
			MAR6 A 0 A_StartSound("strongmelee/miss", CHAN_AUTO);
			MAR6 AB 1;
			MAR6 C 3 A_DiazEE_Melee(40, 70, "StrongMeleePuff");
			MAR6 D 1;
			MART OPP 1;
			Goto AfterMelee;
			
	// P-P-K
		Roundhouse:
			TNT1 A 6 SetPlayerProperty(0,1,PROP_FROZEN);
			MAR2 J 1 A_StartSound("*largemelee", CHAN_VOICE);
			MAR2 KL 1;
			MAR2 M 1 A_StartSound("strongmelee/miss", CHAN_AUTO);
			MAR2 N 1 A_DiazEE_Melee(40, 90, "StrongMeleePuff");
			MAR2 OP 1;
			TNT1 A 6;
			TNT1 A 0 SetPlayerProperty(0, 0, PROP_FROZEN);
			MART WVAAAAAAAA 1 ;
			MART A 8;
			Goto AfterMelee;

	// P-P-P-K, MAP BREAKING JUMPKICK
		MapBreaker:
			MART VW 1;
			TNT1 A 2 SetPlayerProperty(0, 1, PROP_FROZEN);
			TNT1 A 12 ThrustThingZ(0, 30, 0, 1);
			MAR4 AB 1;
			MAR4 C 1 A_StartSound("*largemelee", CHAN_VOICE);
			MAR4 D 1 A_StartSound("strongmelee/miss", CHAN_AUTO);
			MAR4 E 1 A_DiazEE_Melee(80, 90, "StrongMeleePuff");
			MAR4 FGH 1;
			TNT1 A 12 SetPlayerProperty(0, 0, PROP_FROZEN);
			MART WV 1; 
			Goto AfterMelee;

		AfterMelee:
			"----" A 0
			{
				invoker.combo_primary = 0;
				invoker.combo_alt = 0;
				invoker.combo_mach = 0;
			}
			TNT1 A 0 A_TakeInventory("ComboCoolDown", 99);
			Goto Ready;
	}
}