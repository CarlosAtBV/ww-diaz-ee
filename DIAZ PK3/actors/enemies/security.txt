// SMG guy - almost entirely ripped from KDIZD

ACTOR WeaselSMGZombie
{
	Health 30
	Height 56
	Speed 8
	PainChance 200
	MONSTER
	+FLOORCLIP
	DropItem Chainsaw 64//SMG37 64
	DropItem ExtraSMGAmmo 128
	SeeSound "bob/sight"
	AttackSound "enemy/smg"
	PainSound "bob/pain"
	DeathSound "bob/death"
	ActiveSound "combine/idle"
	Obituary "%o was pelted with tiny little bullets."
	Decal BulletChip
	Tag "Military Police"
	States
	{
	Spawn:
		RFTR AB 10 A_Look
		Loop
	See:
		RFTR A 0 A_JumpIfInventory("SMGClip",40,"Reload")
		RFTR AABBCCDD 4 A_Chase
		Loop
	Missile:
		RFTR E 10 A_FaceTarget
		RFTR F 2 Bright A_CustomBulletAttack(8,3,1,3)
		RFTR E 1
		RFTR E 0 A_FaceTarget
		RFTR F 2 Bright A_CustomBulletAttack(8,3,1,3)
		RFTR E 1
		RFTR E 0 A_FaceTarget
		RFTR F 2 Bright A_CustomBulletAttack(8,3,1,3)
		RFTR E 1 A_GiveInventory("SMGClip",3)
		Goto See
	Reload:
		RFTR E 0 A_TakeInventory("SMGClip",40)
		RFTR E 8 A_PlaySound("mp40/clipout")
		RFTR E 8
		RFTR D 16 A_PlaySound("mp40/clipin")
		RFTR E 8 A_PlaySound("mp40/cock")
		goto See
	Pain:
		RFTR G 3
		RFTR G 3 A_Pain
		Goto See
	Death.Lightning:
		LDTH A 2 BRIGHT A_PlaySound("misc/lightningdeath",5)
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
	Death:
		RFTR H 5
		RFTR I 5 A_Scream
		RFTR J 5 A_Fall
		RFTR K 5
		RFTR L -1 A_PlaySound("misc/bodyfall")
		Stop
	XDeath:
		POSS M 0 A_SpawnDebris("NashGore_Gib9",0)
		POSS M 0 A_SpawnDebris("NashGore_Gib10",0)
		POSS M 0 A_SpawnDebris("NashGore_Gib11",0)
		POSS M 0 A_SpawnDebris("NashGore_Gib11",0)
		POSS M 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		RFTR M 5
		POSS N 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		RFTR N 5 A_XScream
		POSS O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		RFTR O 5 A_NoBlocking
		RFTR P 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		RFTR PQRST 5
		RFTR U -1
		stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		stop
	Death.Fire:
		BURN ABC 5 BRIGHT
		BURN D 5 BRIGHT A_PlaySound("misc/flamedeath")
		BURN EFGHIJKLMN 5 BRIGHT
		BURN O 5 BRIGHT A_NoBlocking
		BURN PQRSTU 5 BRIGHT
		BURN V -1
		Stop
	Death.Neut:
		DISR A 3 BRIGHT A_SetTranslucent(0.9,0)
		DISR B 3 BRIGHT A_PlaySound("misc/disintdeath")
		DISR C 3 BRIGHT A_SetTranslucent(0.8,0)
		DISR D 3 BRIGHT A_SetTranslucent(0.7,0)
		DISR E 3 BRIGHT A_SetTranslucent(0.6,0)
		DISR F 3 BRIGHT A_SetTranslucent(0.5,0)
		DISR G 3 BRIGHT A_SetTranslucent(0.4,0)
		DISR H 3 BRIGHT A_SetTranslucent(0.3,0)
		DISR I 3 BRIGHT A_SetTranslucent(0.2,0)
		DISR J 3 BRIGHT A_SetTranslucent(0.1,0)
		Stop
	Raise:
		RFTR KJIH 5
		Goto See
	}
}