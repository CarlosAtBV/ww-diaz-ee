ACTOR WeaselJudgmentZombie : WeaselPistolZombie
{
	Obituary "%o was judged and found wanting."
	TRANSLATION 2
	SeeSound "swat/sight"
	PainSound "swat/pain"
	DeathSound "swat/death"
	ActiveSound "combine/idle"
	AttackSound "judgment/enemy"
	DropItem Clip 128//P50Ammo 128
	DropItem JudgmentPistol 64
	Decal BulletChip
	Speed 9
	Tag "Military Police S.W.A.T."
	States
	{
	Spawn:
		JDGZ AB 10 A_Look
		Loop
	See:
		JDGZ A 0 A_JumpIfInventory("JudgmentClip",12,"Reload")
		JDGZ AABBCCDD 3 A_Chase
		Loop
	Missile:
		JDGZ E 2 A_FaceTarget
		JDGZ F 0 A_CustomMissile("WWPistolCasing",32,10,random(260,280))
		JDGZ F 2 BRIGHT A_CustomBulletAttack(5,2,1,2)
		JDGZ E 3 A_GiveInventory("JudgmentClip",1)
		Goto See
	Reload:
		JDGZ E 0 A_TakeInventory("JudgmentClip",12)
		JDGZ E 8 A_PlaySound("judgment/clipout")
		JDGZ D 12 A_CustomMissile("SpentClip1",32,10,random(260,280))
		JDGZ E 8 A_PlaySound("judgment/clipin")
		JDGZ E 4
		JDGZ D 4 A_PlaySound("judgment/cliphit")
		JDGZ D 5
		JDGZ E 2 A_PlaySound("judgment/slide")
		Goto See
	Pain:
		JDGZ G 4 A_Pain
		JDGZ G 4
		Goto See
	Death.Lightning:
		LDTH A 2 BRIGHT A_PlaySound("misc/lightningdeath",5)
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
	Death:
		JDGZ H 4
		JDGZ I 4 A_Scream
		JDGZ J 4 A_NoBlocking
		JDGZ KLM 4
		JDGZ N -1 A_PlaySound("misc/bodyfall")
		Stop
	XDeath:
		POSS M 0 A_SpawnDebris("NashGore_Gib9",0)
		POSS M 0 A_SpawnDebris("NashGore_Gib10",0)
		POSS M 0 A_SpawnDebris("NashGore_Gib11",0)
		POSS M 0 A_SpawnDebris("NashGore_Gib11",0)
		POSS M 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		JDGZ O 5
		POSS N 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		JDGZ P 5 A_XScream
		POSS O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		JDGZ Q 5 A_NoBlocking
		JDGZ P 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		JDGZ RSTUV 5
		JDGZ W -1
		stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		stop
	Death.Fire:
		BURN ABC 5 BRIGHT
		BURN D 5 BRIGHT A_PlaySound("misc/combineflame")
		BURN EFGHIJKLMN 5 BRIGHT
		BURN O 5 BRIGHT A_NoBlocking
		BURN PQRSTU 5 BRIGHT
		BURN V -1
		Stop
	Death.Neut:
		DISR A 3 BRIGHT A_SetTranslucent(0.9,0)
		DISR B 3 BRIGHT A_PlaySound("misc/disintdeath")
		DISR C 3 BRIGHT A_SetTranslucent(0.8,0)
		DISR D 3 BRIGHT A_SetTranslucent(0.7,0)
		DISR E 3 BRIGHT A_SetTranslucent(0.6,0)
		DISR F 3 BRIGHT A_SetTranslucent(0.5,0)
		DISR G 3 BRIGHT A_SetTranslucent(0.4,0)
		DISR H 3 BRIGHT A_SetTranslucent(0.3,0)
		DISR I 3 BRIGHT A_SetTranslucent(0.2,0)
		DISR J 3 BRIGHT A_SetTranslucent(0.1,0)
		Stop
	Raise:
		JDGZ NMLKJIH 3
		Goto See
	}
}