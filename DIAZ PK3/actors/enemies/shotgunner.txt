// Shotgunner - based on the shotgun soldier from the monster resource wad

ACTOR WeaselShotgunner : ShotgunGuy
{
	Obituary "%o was eliminated for the lesser good."
	TRANSLATION 1
	SeeSound "shotgunner/sighted"
	PainSound "shotgunner/pain"
	DeathSound "shotgunner/death"
	ActiveSound "combine/idle"
	AttackSound "enemy/shot"
	DropItem Shotgun 96//MiniShotgun 96
	DropItem ExtraShotgunAmmo 96
	Decal BulletChip
	Tag "Security Response Team"
	States
	{
	Spawn:
		ZOLD AB 10 A_Look
		Loop
	See:
		ZOLD A 0 A_JumpIfInventory("ShotgunClip",4,"Reload")
		ZOLD AABBCCDD 4 A_Chase
		Loop
	Missile:
		ZOLD DE 3 A_FaceTarget
		ZOLD F 3 BRIGHT A_CustomBulletAttack(10,2,9,1)
		ZOLD E 5 A_GiveInventory("ShotgunClip",1)
		ZOLD E 5 A_PlaySound("mini/cock")
		ZOLD E 0 A_CustomMissile("WWShotgunCasing",32,10,random(260,280))
		ZOLD E 5 A_PlaySound("mini/uncock")
		Goto See
	Reload:
		ZOLD E 8 A_PlaySound("mini/cock")
		ZOLD E 16
		ZOLD D 4 A_PlaySound("mini/load")
		ZOLD E 14 A_TakeInventory("ShotgunClip",1)
		ZOLD D 4 A_PlaySound("mini/load")
		ZOLD E 14 A_TakeInventory("ShotgunClip",1)
		ZOLD D 4 A_PlaySound("mini/load")
		ZOLD E 14 A_TakeInventory("ShotgunClip",1)
		ZOLD D 4 A_PlaySound("mini/load")
		ZOLD E 18 A_TakeInventory("ShotgunClip",1)
		ZOLD E 6 A_PlaySound("mini/uncock")
		Goto See
	Pain:
		ZOLD G 3
		ZOLD G 3 A_Pain
		Goto See
	Death.Lightning:
		LDTH A 2 BRIGHT A_PlaySound("misc/lightningdeath",5)
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
		LDTH A 2 BRIGHT
		LDTH A 2
	Death:
		ZOLD H 5
		ZOLD I 5 A_Scream
		ZOLD J 5 A_Fall
		ZOLD KLM 5
		ZOLD N -1 A_PlaySound("misc/bodyfall")
		Stop
	XDeath:
		POSS M 0 A_SpawnDebris("NashGore_Gib9",0)
		POSS M 0 A_SpawnDebris("NashGore_Gib10",0)
		POSS M 0 A_SpawnDebris("NashGore_Gib11",0)
		POSS M 0 A_SpawnDebris("NashGore_Gib11",0)
		POSS M 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		ZOLD O 5
		POSS N 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		ZOLD P 5 A_XScream
		POSS O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		ZOLD Q 5 A_NoBlocking
		RFTR P 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		ZOLD RSTUV 5
		ZOLD W -1
		stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		stop
	Death.Fire:
		BURN ABC 5 BRIGHT
		BURN D 5 BRIGHT A_PlaySound("misc/flamedeath")
		BURN EFGHIJKLMN 5 BRIGHT
		BURN O 5 BRIGHT A_NoBlocking
		BURN PQRSTU 5 BRIGHT
		BURN V -1
		Stop
	Death.Neut:
		DISR A 3 BRIGHT A_SetTranslucent(0.9,0)
		DISR B 3 BRIGHT A_PlaySound("misc/disintdeath")
		DISR C 3 BRIGHT A_SetTranslucent(0.8,0)
		DISR D 3 BRIGHT A_SetTranslucent(0.7,0)
		DISR E 3 BRIGHT A_SetTranslucent(0.6,0)
		DISR F 3 BRIGHT A_SetTranslucent(0.5,0)
		DISR G 3 BRIGHT A_SetTranslucent(0.4,0)
		DISR H 3 BRIGHT A_SetTranslucent(0.3,0)
		DISR I 3 BRIGHT A_SetTranslucent(0.2,0)
		DISR J 3 BRIGHT A_SetTranslucent(0.1,0)
		Stop
	Raise:
		ZOLD KJIH 5
		Goto See
	}
}