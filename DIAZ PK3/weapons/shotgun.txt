ACTOR ExtraShotgunAmmo : Ammo
{
	//$Category ammunition
	Inventory.MaxAmount 24
	Inventory.PickupMessage "00 buckshot ammo"
	Tag "Shells"
	Inventory.Amount 4
	Inventory.PickupSound "mini/load"
	Inventory.Icon SSHLA0
	Ammo.BackpackAmount 4
	Ammo.BackpackMaxAmount 48
	Scale 0.5
	States
	{
	Spawn:
		SSHL A -1
		LOOP
	}
}

ACTOR ShotgunClip : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 4
	Inventory.Icon MSHPA0
}

ACTOR MiniShotgunReloading : Inventory{}

ACTOR ShotgunSeq : Inventory{}

ACTOR MiniShotgun : Weapon replaces Shotgun
{
	//$Category weapons
	Inventory.PickupMessage "makoto type 305 Mini-Shotgun (3)"
	Tag "Makoto 305"
	Inventory.PickupSound "mini/pickup"
	Obituary "%o can no longer be considered lead-free, thanks to %k."
	Weapon.AmmoType1 "ShotgunClip"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "ExtraShotgunAmmo"
	Weapon.AmmoGive2 6
	Weapon.AmmoUse2 1
	Radius 24
	Height 12
	Scale 0.75
	Weapon.SisterWeapon MiniShotgun2
	+NOEXTREMEDEATH
	+NOAUTOFIRE
	+AMMO_CHECKBOTH
	+NOALERT
	States
	{
	Spawn:
		MSHP A -1
		LOOP
	Deselect:
		MSHH A 0 A_JumpIfInventory("MiniShotgunReloading",1,"ReloadFinish2")
		MSHH AB 1
		MSHH C 1 A_StartSound("weapon/deselect", CHAN_AUTO)
		MSHH DE 1
		TNT1 A 16
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_Raise
		Loop
	Ready:
		TNT1 A 16
		MSHH ED 1
		MSHH C 1 A_StartSound("minishotgun/equip", CHAN_AUTO)
		MSHH BA 1
	Ready2:
		MSHT A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		MSHT A 0 A_JumpIfInventory("MiniShotgunReloading",1,"ReloadFinish")
		MSHT A 0 A_JumpIfInventory("ShotgunSeq",1,"Pump")
		MSHT A 0 A_JumpIfInventory("ShotgunClip",1,1)
		Goto DryFire
		MSHF A 0 BRIGHT A_GunFlash
		MSHF A 0 BRIGHT A_StartSound("minishotgun/fire", CHAN_AUTO)
		MSHF A 1 BRIGHT A_FireBullets(5,3,12,4,"ShotPuff",1)
		MSHF A 1 A_SetPitch(pitch - 4.0)
		MSHF A 0 A_SetAngle(angle + (frandom(0.2,0.4)))
		MSHT C 1 A_FireCustomMissile("ShotSmokeSpawner",0,0,4,2)
		MSHT B 1 A_SetPitch(pitch - 4.0)
		MSHT A 3 A_AlertMonsters
		MSHT A 0 A_GiveInventory("ShotgunSeq")
		TNT1 A 0 A_CheckReload
		Goto Ready2
	Pump:
		MSHT B 1 A_StartSound("minishotgun/cock/in", CHAN_AUTO)
		MSHT C 1
		MSHT D 5 A_FireCustomMissile("ShellCasingSpawner",-3,0,8,-6)
		MSHT C 1 A_StartSound("minishotgun/cock/out", CHAN_AUTO)
		MSHT B 1
		MSHT A 3 A_TakeInventory("ShotgunSeq")
		Goto Ready2
	DryFire:
		MSHT A 1
		MSHT A 4 A_StartSound("minishotgun/empty", CHAN_AUTO)
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		Goto Ready2
	Reload:
		TNT1 A 0 A_JumpIfInventory("ShotgunClip",0,2)
		TNT1 A 0 A_JumpIfInventory("ExtraShotgunAmmo",1,2)
		MSHT A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
		MSHT A 0 A_GiveInventory("MiniShotgunReloading",1)
		MSHT A 0 A_JumpIfInventory("ShotgunSeq",1,"AltReloadStart")
		MSHT B 1 A_StartSound("minishotgun/cock/in", CHAN_AUTO)
		MSHT CD 1
		MSHT E 8
		Goto ReloadWork
	AltReloadStart:
		MSHT B 1 A_StartSound("minishotgun/cock/in", CHAN_AUTO)
		MSHT C 1
		MSHT D 1 A_FireCustomMissile("ShellCasingSpawner",-3,0,8,-6)
		MSHT E 8
	ReloadWork:
		MSHT A 0 A_TakeInventory("ExtraShotgunAmmo",1)
		MSHT A 0 A_GiveInventory("ShotgunClip")
		MSHT B 0 A_StartSound("minishotgun/load", CHAN_AUTO)
		MSHT FG 3
		MSHT E 8 A_WeaponReady(WRF_NOBOB)
		ASDS A 0 A_JumpIfInventory("ShotgunClip",0,"ReloadFinish")
		ASDS A 0 A_JumpIfInventory("ExtraShotgunAmmo",1,"ReloadWork")
	ReloadFinish:
		MSHT D 1 A_StartSound("minishotgun/cock/out", CHAN_AUTO)
		MSHT CB 1
		MSHT A 3 A_TakeInventory("MiniShotgunReloading")
		MSHT A 0 A_TakeInventory("ShotgunSeq")
		Goto Ready2
	ReloadFinish2:
		MSHT D 1 A_StartSound("minishotgun/cock/out", CHAN_AUTO)
		MSHT CB 1
		MSHT A 3 A_TakeInventory("MiniShotgunReloading")
		MSHT A 0 A_TakeInventory("ShotgunSeq")
		Goto Deselect
	AltFire:
		MSHM A 3
		MSHM B 2 A_PlayWeaponSound("melee/swing")
		MSHM C 1
		MSHM D 1 A_CustomPunch(6,0,0,"hitpuff")
		MSHM EFG 1
		MSHM HIJKA 2
		Goto Ready2
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

ACTOR MiniShotgun2 : MiniShotgun
{
	Weapon.SisterWeapon MiniShotgun
	+POWERED_UP
	Obituary "%o wonders how %k got that shotgun to fire so fast."
	Weapon.AmmoGive2 0
	States
	{
	Pump:
		MSHT B 1 A_PlaySound("mini/cock")
		MSHT C 1
		MSHT D 3 A_FireCustomMissile("ShellCasingSpawner",-3,0,8,-6)
		MSHT C 1 A_PlaySound("mini/uncock")
		MSHT B 1
		MSHT A 2 A_TakeInventory("ShotgunSeq",1)
		Goto Ready2
	Reload:
		TNT1 A 0 A_JumpIfInventory("ShotgunClip",0,2)
		TNT1 A 0 A_JumpIfInventory("ExtraShotgunAmmo",1,2)
		MSHT A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
		MSHT A 0 A_JumpIfInventory("ShotgunSeq",1,"AltReloadStart")
		MSHT A 0 A_GiveInventory("MiniShotgunReloading")
		MSHT B 1 A_PlaySound("mini/cock")
		MSHT CD 1
		MSHT E 5
		Goto ReloadWork
	AltReloadStart:
		MSHT A 0 A_GiveInventory("MiniShotgunReloading")
		MSHT B 1 A_PlaySound("mini/cock")
		MSHT C 1
		MSHT D 1 A_FireCustomMissile("ShellCasingSpawner",-3,0,8,-6)
		MSHT E 5
	ReloadWork:
		MSHT A 0 A_TakeInventory("ExtraShotgunAmmo",1)
		MSHT A 0 A_GiveInventory("ShotgunClip")
		MSHT B 0 A_PlayWeaponSound("mini/load")
		MSHT FG 2
		MSHT E 6 A_WeaponReady(WRF_NOBOB)
		ASDS A 0 A_JumpIfInventory("ShotgunClip",4,"ReloadFinish")
		ASDS A 0 A_JumpIfInventory("ExtraShotgunAmmo",1,"ReloadWork")
	ReloadFinish:
		MSHT D 1 A_PlaySound("mini/uncock")
		MSHT CB 1
		MSHT A 2 A_TakeInventory("MiniShotgunReloading")
		MSHT A 0 A_TakeInventory("ShotgunSeq")
		Goto Ready2
	ReloadFinish2:
		MSHT D 1 A_PlaySound("mini/uncock")
		MSHT CB 1
		MSHT A 2 A_TakeInventory("MiniShotgunReloading")
		MSHT A 0 A_TakeInventory("ShotgunSeq")
		Goto Deselect
	AltFire:
		MSHM A 3
		MSHM B 2 A_PlayWeaponSound("melee/swing")
		MSHM C 1
		MSHM D 1 A_CustomPunch(24,0,0,"powerhitpuff")
		MSHM EFG 1
		MSHM HIJKA 2
		Goto Ready2
	}
}