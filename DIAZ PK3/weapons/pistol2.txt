ACTOR JudgmentPistol : Weapon
{
	//$Category weapons
	Inventory.PickupMessage "Judgment Mark II Pistol (2)"
	Tag "Judgment Mk.2"
	Inventory.PickupSound "judgment/cock"
	Obituary "%o was sissied to death by %k's nickel pistol."
	Weapon.AmmoType "JudgmentClip"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "P50Ammo"
	Weapon.AmmoGive2 2
	Weapon.AmmoUse2 1
	Radius 12
	Height 12
	Weapon.SisterWeapon JudgmentPistol2
	Scale 0.75
	+AMMO_CHECKBOTH
	+NOEXTREMEDEATH
	+NOAUTOFIRE
	+NOALERT
	+INVENTORY.UNDROPPABLE
	States
	{
	Spawn:
		JUDP A -1
		Loop
    Deselect:
		JDGH C 1 A_PlaySound("weapon/deselect")
		JDGH BA 1
		TNT1 A 4
    	TNT1 A 0 A_Lower
    	Wait
	Select:
		TNT1 A 0 A_Raise
		Loop
	Ready:
		TNT1 A 4
		JDGH AB 1
		JDGH C 1 A_PlaySound("weapon/select3")
		JDGC ABCD 1
		JDGC E 1 A_PlayWeaponSound("judgment/cock")
		JDGC FFGGHIJK 1
    Ready2:
		JDGG E 0 A_JumpIfInventory("JudgmentClip",1,2)
		JDGG E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
    	JDGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    	Loop
	Fire:
		JDGG A 0 A_JumpIfInventory("JudgmentClip",1,1)
		Goto DryFire
		JDGF A 1 A_AlertMonsters
		JDGF B 0 A_SetPitch(pitch - 1.0)
		JDGF B 0 A_PlayWeaponSound("judgment/fire")
		JDGF B 1 BRIGHT A_FireBullets(1,1,-1,6.5,"BulletPuff",1)
		JDGF C 1 BRIGHT A_FireCustomMissile("PistolSmokeSpawner",0,0,3,8)
    	JDGG D 1 BRIGHT A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
		JDGG E 1 A_JumpIfInventory("JudgmentClip",1,1)
		Goto Slideback
 		JDGG C 1
		JDGG BA 1 A_WeaponReady
		TNT1 A 0 A_CheckReload
		Goto Ready2
	Dryfire:
		JDGG E 2 A_PlayWeaponSound("judgment/empty")
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		Goto Ready2
	Slideback:
		JDGG E 2 A_PlaySound("judgment/slideback")
		TNT1 A 0 A_CheckReload
		Goto Ready2
	Reload:
    	TNT1 A 0 A_JumpIfInventory("JudgmentClip",0,2)
    	TNT1 A 0 A_JumpIfInventory("P50Ammo",1,2)
      	JDGG "#" 1 A_WeaponReady(WRF_NOFIRE)
      	Goto Ready2
		JDGR A 0 A_JumpIfInventory("JudgmentClip",1,"ReloadFull")
		JDGR A 0 A_PlayWeaponSound("judgment/clipout")
		JDGR A 1 A_FireCustomMissile("SpentClip1",3,0,5,0)
		JDGR BCCCCCCCCCCCCCCCDDE 1
		TNT1 A 0 A_TakeInventory("P50Ammo",1)
		TNT1 A 0 A_GiveInventory("JudgmentClip",12)
		JDGR F 1 A_PlayWeaponSound("judgment/clipin")
		JDGR GHIJ 1
		JDGR K 1 A_PlayWeaponSound("judgment/cliphit")
		JDGR LMNO 1
		JDGR P 1 A_PlaySound("judgment/slide",5)
		JDGR QRST 1
		Goto Ready2
	ReloadFull:
		JDGG A 3 A_PlayWeaponSound("judgment/clipout")
		JDGS A 1 A_FireCustomMissile("SpentClip1",3,0,5,0)
		JDGS BCCCCCCCCCCCCCCCDDE 1
		JDGS E 0 A_TakeInventory("P50Ammo",1)
		JDGS E 0 A_GiveInventory("JudgmentClip",12)
		JDGS F 1 A_PlayWeaponSound("judgment/clipin")
		JDGS GHH 1
		JDGS K 1 A_PlayWeaponSound("judgment/cliphit")
		JDGS LMRST 1
		Goto Ready2
	AltFire:
		JDGM ABC 1
      	TNT1 A 3
		JDGM A 0 A_JumpIfInventory("JudgmentClip",1,"AltfireFull")
      	JDGM I 1 A_PlaySound("melee/swing")
		JDGM J 1
      	JDGM K 1 A_CustomPunch(6.5,0,0,"hitpuff")
      	JDGM LM 1
		TNT1 A 4
		JDGH ABC 1
      	Goto Ready2
	AltfireFull:
      	JDGM D 1 A_PlaySound("melee/swing")
		JDGM E 1
      	JDGM F 1 A_CustomPunch(7,0,0,"hitpuff")
      	JDGM GH 1
		TNT1 A 4
		JDGH ABC 1
      	Goto Ready2
    }
}

ACTOR JudgmentPistol2 : JudgmentPistol
{
	Weapon.SisterWeapon JudgmentPistol
	Weapon.AmmoGive2 0
	+POWERED_UP
	States
	{
	Reload:
    	TNT1 A 0 A_JumpIfInventory("JudgmentClip",0,2)
    	TNT1 A 0 A_JumpIfInventory("P50Ammo",1,2)
      	JDGG "#" 1 A_WeaponReady(WRF_NOFIRE)
      	Goto Ready2
		JDGR a 0 A_PlayWeaponSound("judgment/clipout")
		JDGR a 0 A_JumpIfInventory("JudgmentClip",1,"ReloadFull")
		JDGR A 1 A_FireCustomMissile("SpentClip1",3,0,5,0)
		JDGR BCCCDDE 1
		TNT1 A 0 A_TakeInventory("P50Ammo",1)
		TNT1 A 0 A_GiveInventory("JudgmentClip",12)
		JDGR F 1 A_PlayWeaponSound("judgment/clipin")
		JDGR HJ 1
		JDGR K 1 A_PlayWeaponSound("judgment/cliphit")
		JDGR LN 1
		JDGR P 1 A_PlaySound("judgment/slide",5)
		JDGR RT 1
		Goto Ready2
	ReloadFull:
		JDGG A 2
		JDGS A 1 A_FireCustomMissile("SpentClip1",3,0,5,0)
		JDGS BCCCDDE 1
		JDGS E 0 A_TakeInventory("P50Ammo",1)
		JDGS E 0 A_GiveInventory("JudgmentClip",12)
		JDGS F 1 A_PlayWeaponSound("judgment/clipin")
		JDGS H 1
		JDGS K 1 A_PlayWeaponSound("judgment/cliphit")
		JDGS MST 1
		Goto Ready2
	AltFire:
      	JDGM ABC 1
      	TNT1 A 3
      	JDGM D 1 A_PlaySound("melee/swing")
		JDGM E 1
      	JDGM F 1 A_CustomPunch(22,0,0,"powerhitpuff")
      	JDGM GH 1
		TNT1 A 4
      	JDGH ABC 1
      	Goto Ready2
	}
}

ACTOR JudgmentClip : Ammo
{
	Inventory.MaxAmount 12
	Inventory.Icon JUDPA0
}