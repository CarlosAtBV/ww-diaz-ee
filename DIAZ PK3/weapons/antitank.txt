ACTOR ATAmmo : Ammo
{
	Inventory.MaxAmount 5
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 10
	Inventory.Icon ATGAA0
	Inventory.PickupMessage "15.4mm AT30 ammo"
	Tag "HE Slugs"
	Inventory.PickupSound "weapons/slugout"
	States
	{
	Spawn:
		ATGA A -1
		Stop
	}
}

ACTOR ATClip : Ammo
{
	Inventory.MaxAmount 4
	Inventory.Icon ATGPA0
}

ACTOR SlugRifle : Weapon
{
	Inventory.PickupMessage "k&s at30 slugthrower (5)"
	Tag "K&S AT30"
	Obituary "%o was mistaken for a heavy vehicle by %k."
	Inventory.PickupSound "slugthrower/reload/slap"
	Weapon.AmmoType "ATClip"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "ATAmmo"
	Weapon.AmmoGive2 2
	Weapon.AmmoUse2 1
	Weapon.SisterWeapon SlugRifle2
	Radius 12
	Height 12
	+AMMO_CHECKBOTH
	+NOALERT
	+NOAUTOFIRE
	States
	{
	Spawn:
		ATGP A -1
		Stop
    Deselect:
		ATGH A 1 A_PlaySound("weapon/deselect")
		ATGH BCDE 1
		TNT1 A 12
    	TNT1 A 0 A_Lower
    	Wait
	Select:
		TNT1 A 0 A_Raise
		Loop
	Ready:
		TNT1 A 12
		ATGH E 1 A_PlaySound("weapon/select6")
		ATGH DCBA 1
	Ready2:
		ATGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		ATGG A 0 A_JumpIfInventory("ATClip",1,1)
		Goto DryFire
		ATGG D 0 A_StartSound("slugthrower/fire", CHAN_AUTO)
		ATGG D 1 BRIGHT A_FireCustomMissile("AT30Slug",0,1,8,0)
		ATGG D 0 BRIGHT A_SetPitch(pitch - 7.0)//ACS_Execute(287,0,25,random(2,4),0)
		ATGG D 0 A_FireCustomMissile("ChainSmokeSpawner",0,0,6,8)
		ATGF A 1 BRIGHT A_GunFlash
		ATGF B 1 BRIGHT A_AlertMonsters
		ATGG BCD 1
		ATGG E 1 A_FireCustomMissile("DiazRifleCasingSpawner2",-3,0,0,0)
		ATGG A 3
		TNT1 A 0 A_CheckReload
		Goto Ready2
	DryFire:
		ATGG E 1 A_StartSound("slugthrower/empty", CHAN_AUTO)
		ATGG A 5
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		Goto Ready2
	Reload:
		ATGG A 0 A_JumpIfInventory("ATClip",4,2)
		ATGG A 0 A_JumpIfInventory("ATAmmo",1,2)
		ATGG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
		ATGR ABCD 2
		ATGR E 2 A_StartSound("slugthrower/reload/out", CHAN_AUTO)
		ATGR F 2 A_FireCustomMissile("SpentClip3",3,0,5,0)
		ATGR GHHHHH 2
		ATGR H 2 A_StartSound("slugthrower/reload/in", CHAN_AUTO)
		ATGR HHHGF 2
		TNT1 A 0 A_TakeInventory("ATAmmo",1)
		TNT1 A 0 A_GiveInventory("ATClip",4)
		ATGR IJKLMNO 2
		ATGR P 2 A_StartSound("slugthrower/reload/slap", CHAN_AUTO)
		ATGR QRSTU 2
		Goto Ready2
	Flash:
      	TNT1 A 2 A_Light2
      	TNT1 A 2 A_Light1
      	Goto LightDone
	Altfire:
		ATGH ABCD 1
		ATGH E 1
		TNT1 A 4
		TNT1 A 3 A_PlayWeaponSound("melee/swing")
		ATGM A 1
		ATGM B 1 A_CustomPunch(8,0,0,"HitPuff")
		ATGM CDEF 1
		TNT1 A 5
		ATGH EDCBBAA 1
		Goto Ready2
	}
}

ACTOR SlugRifle2 : SlugRifle
{
	Weapon.SisterWeapon SlugRifle
	Weapon.AmmoGive2 0
	+POWERED_UP
	Obituary "%o was mistaken for a large building by %k."
	States
	{
	Reload:
		ATGG A 0 A_JumpIfInventory("ATClip",3,2)
		ATGG A 0 A_JumpIfInventory("ATAmmo",1,2)
		ATGG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
		ATGR ABCD 1
		ATGR E 1 A_PlayWeaponSound("weapons/slugout")
		TNT1 A 0 A_TakeInventory("ATClip",3)
		ATGR F 1 A_FireCustomMissile("SpentClip3",3,0,5,0)
		ATGR GHHHHH 1
		ATGR H 1 A_PlayWeaponSound("weapons/slugin")
		ATGR HHHGF 1
		TNT1 A 0 A_TakeInventory("ATAmmo",1)
		TNT1 A 0 A_GiveInventory("ATClip",4)
		ATGR IJKLMNO 1
		ATGR P 1 A_PlayWeaponSound("weapons/slugslap")
		ATGR QRSTU 1
		Goto Ready2
	Altfire:
		ATGH ABCD 1
		ATGH E 1 A_PlayWeaponSound("melee/swing")
		TNT1 A 7
		ATGM A 1
		ATGM B 1 A_CustomPunch(32,0,0,"HitPuff")
		ATGM CDEF 1
		TNT1 A 5
		ATGH EDCBBAA 1
		Goto Ready2
	}
}